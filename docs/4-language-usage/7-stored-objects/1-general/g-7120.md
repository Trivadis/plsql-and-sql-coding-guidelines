# G-7120: Always add the name of the program unit to its end keyword.

!!! tip "Minor"
    Maintainability

## Reason

It's a good alternative for comments to indicate the end of program units, especially if they are lengthy or nested.

## Example (bad)

``` sql
create or replace package body employee_api is
   function employee_by_id(in_employee_id in employees.employee_id%type)
      return employees%rowtype is
      r_employee employees%rowtype;
   begin
      select *
        into r_employee
        from employees
       where employee_id = in_employee_id;

      return r_employee;
   exception
      when no_data_found then
         null;
      when too_many_rows then
         raise;
   end;
end;
/
```

## Example (good)

``` sql
create or replace package body employee_api is
   function employee_by_id(in_employee_id in employees.employee_id%type)
      return employees%rowtype is
      r_employee employees%rowtype;
   begin
      select *
        into r_employee
        from employees
       where employee_id = in_employee_id;

      return r_employee;
   exception
      when no_data_found then
         null;
      when too_many_rows then
         raise;
   end employee_by_id;
end employee_api;
/
```