# G-4320: Always label your loops.

!!! tip "Minor"
    Maintainability

## Reason

It's a good alternative for comments to indicate the start and end of a named loop processing.

## Example (bad)

``` sql hl_lines="8 11 13 15 17 20 22 25"
declare
   i            integer;
   co_min_value constant simple_integer := 1;
   co_max_value constant simple_integer := 10;
   co_increment constant simple_integer := 1;
begin
   i := co_min_value;
   while (i <= co_max_value)
   loop
      i := i + co_increment;
   end loop;

   loop
      exit when true; -- NOSONAR: G-4380 cannot use labelled exit here
   end loop;

   for i in co_min_value..co_max_value
   loop
      sys.dbms_output.put_line(i);
   end loop;

   for r_employee in (select last_name from employees)
   loop
      sys.dbms_output.put_line(r_employee.last_name);
   end loop;
end;
/
```

## Example (good)

``` sql hl_lines="8 12 14 17 19 23 25 29"
declare
   i            integer;
   co_min_value constant simple_integer := 1;
   co_max_value constant simple_integer := 10;
   co_increment constant simple_integer := 1;
begin
   i := co_min_value;
   <<while_loop>>
   while (i <= co_max_value)
   loop
      i := i + co_increment;
   end loop while_loop;

   <<basic_loop>>
   loop
      exit basic_loop when true;
   end loop basic_loop;

   <<for_loop>>
   for i in co_min_value..co_max_value
   loop
      sys.dbms_output.put_line(i);
   end loop for_loop;

   <<process_employees>>
   for r_employee in (select last_name from employees)
   loop
      sys.dbms_output.put_line(r_employee.last_name);
   end loop process_employees;
end;
/
```