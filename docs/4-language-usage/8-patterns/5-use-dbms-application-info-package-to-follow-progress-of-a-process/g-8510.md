# G-8510: Always use dbms_application_info to track program process transiently.

!!! tip "Minor"
    Efficiency, Reliability

## Reason

This technique allows us to view progress of a process without having to persistently write log data in either a table or a file. The information is accessible through the `V$SESSION` view.

## Example (bad)

``` sql
create or replace package body employee_api is
   procedure process_emps is
   begin
      <<employees>>
      for emp_rec in (select employee_id
                        from employees
                       order by employee_id)
      loop
         null; -- some processing
      end loop employees;
   end process_emps;
end employee_api;
/
```

## Example (good)

``` sql
create or replace package body employee_api is
   procedure process_emps is
   begin
      sys.dbms_application_info.set_module(module_name => $$plsql_unit
                                          ,action_name => 'Init');
      <<employees>>
      for emp_rec in (select employee_id 
                        from employees
                       order by employee_id)
      loop
         sys.dbms_application_info.set_action('Processing ' || emp_rec.employee_id);
      end loop employees;
   end process_emps;
end employee_api;
/
```